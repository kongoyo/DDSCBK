     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A          R TAPI01                    SFL
     A  64                                  SFLNXTCHG
     A            OPT            1A  I  9  4
     A            RDMID          6A  O  9 10
     A            RDVOL          6A  O  9 20
     A            RDMTYP         2A  O  9 27
     A            RDLIB         10A  O  9 33
     A            RDLOC         10A  O  9 44
     A            RDLOCI         1A  O  9 55
     A            RDWPR          1A  O  9 65
     A          R TAPI02                    SFLCTL(TAPI01)
     A                                      SFLSIZ(0080)
     A                                      SFLPAG(0010)
     A                                      CA03(03 'F3=Exit')
     A                                      CA05(05 'F5=Refresh Screen')
     A                                      CA12(12 'F12=Cancel')
     A  95                                  CA06
     A                                      CA07
     A                                      CA08
     A  39                                  PAGEDOWN(43)
     A  39                                  PAGEUP(44)
     A                                      LOCK
     A                                      OVERLAY
     A  60                                  SFLDSP
     A  61                                  SFLDSPCTL
     A  62                                  SFLCLR
     A N63                                  SFLEND(*MORE)
     A                                  2 36'Tape Table'
     A                                      COLOR(WHT)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2  4'System:'
     A                                  2 12SYSNAME
     A                                  2 60DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3 26'Listing all Tapes Information'
     A                                  7 10'Cartridge'
     A                                      COLOR(BLU)
     A                                  8 10'ID'
     A                                      COLOR(BLU)
     A                                  7 20'Volume'
     A                                      COLOR(BLU)
     A                                  8 20'ID'
     A                                      COLOR(BLU)
     A                                  7 27'Media'
     A                                      COLOR(BLU)
     A                                  8 27'Type'
     A                                      COLOR(BLU)
     A                                  7 33'Media'
     A                                      COLOR(BLU)
     A                                  8 33'Library'
     A                                      COLOR(BLU)
     A                                  7 44'Driver'
     A                                      COLOR(BLU)
     A                                  8 44'Location'
     A                                      COLOR(BLU)
     A                                  7 55'Driver'
     A                                      COLOR(BLU)
     A                                  8 55'Indicator'
     A                                      COLOR(BLU)
     A                                  7 65'Write'
     A                                      COLOR(BLU)
     A                                  8 65'Protection'
     A                                      COLOR(BLU)
     A                                  8  4'Opt'
     A                                      COLOR(BLU)
     A                                  5  4'Opt[I] = Initialize'
     A                                      COLOR(WHT)
     A                                  6  4'Opt[P] = Print'
     A                                      COLOR(WHT)
     A                                  6 24'Opt[M] = Mount'
     A                                      COLOR(WHT)
     A                                  3  4'User  :'
     A                                  3 12USER
     A          R TAPI03
     A                                      LOCK
     A                                      OVERLAY
     A                                 23  4'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 13'F5=Refresh Screen'
     A                                      COLOR(BLU)
     A                                 23 33'F6=Add Record'
     A                                      COLOR(BLU)
     A                                 23 49'F7=Create SPLF'
     A                                      COLOR(BLU)
     A                                 23 66'F8=Tapes SPLF'
     A                                      COLOR(BLU)
     A          R TAPI04
     A  99                                  ALARM
     A  99        MSG           45A  O  4 24COLOR(RED)
     A  99                                  DSPATR(RI)
     A          R INZREC
     A                                      WINDOW(5 22 12 34)
     A                                      CA12(12 'Cancel')
     A                                      WDWBORDER((*CHAR '#-#||#-#'))
     A                                  3  2'Cartridge ID  :'
     A                                  5  2'Media Library :'
     A                                  1  1'        Confirm Initialize        '
     A                                      COLOR(WHT)
     A                                      DSPATR(RI)
     A            INZMSG        30A  O  9  3COLOR(RED)
     A                                 11  2'Enter = Confirm'
     A                                      COLOR(WHT)
     A                                 11 21'F12 = Cancel'
     A                                      COLOR(WHT)
     A            RDMID     R        O  3 18
     A            RDLIB     R        O  5 18
     A          R MNTREC
     A                                      WINDOW(5 22 12 34)
     A                                      CA12(12 'Cancel')
     A                                      WDWBORDER((*CHAR '#-#||#-#'))
     A                                  3  2'Cartridge ID  :'
     A                                  5  2'Media Library :'
     A                                  1  1'         Confirm Mounted          '
     A                                      COLOR(WHT)
     A                                      DSPATR(RI)
     A            MNTMSG        30A  O  9  3COLOR(RED)
     A            RDMID     R        O  3 18
     A            RDLIB     R        O  5 18
     A                                 11  2'Enter = Confirm'
     A                                      COLOR(WHT)
     A                                 11 21'F12 = Cancel'
     A                                      COLOR(WHT)
     A          R PRTREC
     A                                      WINDOW(5 22 12 34)
     A                                      CA12(12 'Cancel')
     A                                      WDWBORDER((*CHAR '#-#||#-#'))
     A                                  3  2'Cartridge ID  :'
     A                                  5  2'Media Library :'
     A                                  1  1'         Confirm Printed          '
     A                                      COLOR(WHT)
     A                                      DSPATR(RI)
     A            PRTMSG        30A  O  9  3COLOR(RED)
     A            RDMID     R        O  3 18
     A            RDLIB     R        O  5 18
     A                                 11  2'Enter = Confirm'
     A                                      COLOR(WHT)
     A                                 11 21'F12 = Cancel'
     A                                      COLOR(WHT)

